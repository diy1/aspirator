# Copyright (c) 2008-2010, Intel Corporation.
# Copyright (c) 2006-2007, The Trustees of Stanford University.
# All rights reserved.
# Licensed under the terms of the New BSD License.

# Author: Mayur Naik (mhn@cs.stanford.edu)
# name=datarace-dlog

.include "A.dom"
.include "F.dom"
.include "E.dom"
.include "H.dom"
.include "M.dom"
.include "L.dom"
.include "K.dom"

.bddvarorder L0_F0_E0_E1_M0_A0xA1_H0_K0

###
# Relations
###

writeE(e:E0) input
ME(m:M0,e:E0) input
EF(e:E0,f:F0) input
statF(f:F0) input
reachableAM(a:A0,m:M0) input
checkExcludedM(m:M0) input
excludeInitMethods(k:K0) input
initM(m:M0) input

statE(e:E0)
rdOrWrAEF(a:A0,e:E0,f:F0)
onlyWrAEF(a:A0,e:E0,f:F0)
relevantAM(a:A0,m:M0)
relevantAE(a:A0,e:E0)
excludeInitM(m:M0)

###

startingRace(a1:A0,e1:E0,a2:A1,e2:E1)

###

EH(e:E0,h:H0) input
escapingRace(a1:A0,e1:E0,a2:A1,e2:E1) output

###

mhe(e:E0,a1:A0,a2:A1) input
parallelRace(a1:A0,e1:E0,a2:A1,e2:E1) output

###

syncLH(l:L,h:H) input
unlockedE(t:A0,e:E0,o:H0) input
excludeSameThread(k:K0) input
guardedE(t:A0,e:E0,o:H0)
unlikelyRace(a1:A0,e1:E0,a2:A1,e2:E1) output

ultimateRace(a1:A0,e1:E0,a2:A1,e2:E1) output
raceEEH(e1:E0,e2:E1,h:H0) output

###
# Constraints
###

excludeInitM(m) :- excludeInitMethods(1), initM(m).

relevantAM(a,m) :- reachableAM(a,m), !checkExcludedM(m), !excludeInitM(m).

relevantAE(a,e) :- relevantAM(a,m), ME(m,e).

rdOrWrAEF(a,e,f) :- relevantAE(a,e), EF(e,f).
onlyWrAEF(a,e,f) :- relevantAE(a,e), EF(e,f), writeE(e).

startingRace(a1,e1,a2,e2) :- onlyWrAEF(a1,e1,f), rdOrWrAEF(a2,e2,f), e1 < e2.
startingRace(a1,e1,a2,e2) :- rdOrWrAEF(a1,e1,f), onlyWrAEF(a2,e2,f), e1 < e2.
startingRace(a1,e1,a2,e2) :- onlyWrAEF(a1,e1,f), onlyWrAEF(a2,e2,f), e1 = e2, a1 <= a2.

###

statE(e) :- EF(e,f), statF(f).

escapingRace(a1,e1,a2,e2) :- startingRace(a1,e1,a2,e2), EH(e1,h), EH(e2,h).
escapingRace(a1,e1,a2,e2) :- startingRace(a1,e1,a2,e2), statE(e1), statE(e2).

###

parallelRace(a1,e1,a2,e2) :- escapingRace(a1,e1,a2,e2), mhe(e1,a1,a2), mhe(e2,a2,a1).

###

syncH(h) :- syncLH(_,h).
guardedE(t,e,h) :- relevantAE(t,e), syncH(h), !unlockedE(t,e,h).

unlikelyRace(a1,e1,a2,e2) :- parallelRace(a1,e1,a2,e2), guardedE(a1,e1,h), guardedE(a2,e2,h).
unlikelyRace(a1,e1,a2,e2) :- parallelRace(a1,e1,a2,e2), excludeSameThread(1), a1=a2.

ultimateRace(a1,e1,a2,e2) :- parallelRace(a1,e1,a2,e2), !unlikelyRace(a1,e1,a2,e2).

raceEEH(e1,e2,h) :- ultimateRace(_,e1,_,e2), EH(e1,h), EH(e2,h).

